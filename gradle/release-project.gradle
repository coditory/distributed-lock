apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'io.github.gradle-nexus.publish-plugin'

jar {
    from(rootProject.projectDir) {
        include "LICENSE"
        into "META-INF"
    }
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

// Sonatype does not support Gradle Module Metadata
// See: https://discuss.gradle.org/t/unable-to-publish-artifact-to-mavencentral/33727/3
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
            artifact sourcesJar
            artifact javadocJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = archivesBaseName
                description = project.description ?: rootProject.description
                url = 'https://github.com/coditory/sherlock-distributed-lock'
                organization {
                    name = "Coditory"
                    url = "https://coditory.com"
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'pawel.mendelski'
                        name = 'Pawel Mendelski'
                        email = 'mendelski.pawel@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:coditory/sherlock-distributed-lock.git'
                    developerConnection = 'scm:git@github.com:coditory/sherlock-distributed-lock.git'
                    url = 'https://github.com/coditory/sherlock-distributed-lock'
                }
                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/coditory/sherlock-distributed-lock/issues"
                }
            }
        }
    }
}

signing {
    if (System.getenv('SIGNING_KEY') && System.getenv('SIGNING_PASSWORD')) {
        useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASSWORD'))
    }
    sign publishing.publications.mavenJava
}

nexusPublishing {
    connectTimeout = Duration.ofMinutes(5)
    clientTimeout = Duration.ofMinutes(5)
    repositories {
        sonatype {
            username = project.findProperty('nexusUsername') ?: System.getenv('NEXUS_USERNAME')
            password = project.findProperty('nexusPassword') ?: System.getenv('NEXUS_PASSWORD')
        }
    }
}
