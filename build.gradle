buildscript {
    ext {
        versions = [
                reactor        : '3.3.9.RELEASE',
                rxjava         : '2.2.19',
                mongodb        : '4.1.0',
                mongodbReactive: '4.1.0',
                slf4j          : '1.7.30',
                logback        : '1.2.3',
                postgresql     : '42.2.16',
                mysql          : '8.0.21',
                // test dependencies:
                groovy         : '3.0.5',
                testContainers : '1.16.1',
                spock          : '2.0-M3-groovy-3.0'
        ]
        notPublishableModules = [
                ':api',
                ':sample',
                ':mongo',
                ':tests'
        ]
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'jacoco'
    id 'signing'
    id 'com.github.kt3k.coveralls' version '2.12.0'
    id 'com.coditory.build' version '0.1.13' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0' apply false
}

description = 'Distributed Lock Library for JVM'

allprojects {
    group = 'com.coditory.sherlock'
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'com.coditory.build'
    sourceCompatibility = 11

    // make nicer archive and module names
    archivesBaseName = rootProject.name + '-' + project.name
    ext.moduleName = archivesBaseName.replaceAll('-', '.')

    dependencies {
        testCompile "ch.qos.logback:logback-core:$versions.logback"
        testCompile "ch.qos.logback:logback-classic:$versions.logback"
        testCompile "org.codehaus.groovy:groovy-all:$versions.groovy"
        testCompile "org.spockframework:spock-core:$versions.spock"
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }

    javadoc {
        source = sourceSets.main.allJava
        classpath = configurations.compileClasspath
        failOnError = false
        options {
            memberLevel = JavadocMemberLevel.PUBLIC
            author = true
        }
    }
}

apply from: "gradle/coverage.gradle"

if (project.hasProperty("release") && project.property("release") == "true") {
    apply from: "gradle/release.gradle"
}
