buildscript {
    ext {
        versions = [
                reactor        : '3.5.2',
                rxjava         : '2.2.21',
                mongodb        : '4.7.2',
                mongodbReactive: '4.8.2',
                slf4j          : '2.0.6',
                logback        : '1.4.5',
                postgresql     : '42.5.3',
                mysql          : '8.0.32',
                // test dependencies:
                jsonAssert     : '1.5.1',
                testContainers : '1.17.6',
                hikaricp       : '5.0.1',
                spock          : '2.3-groovy-4.0'
        ]
        notPublishableModules = [
                ':api',
                ':sample',
                ':mongo',
                ':tests'
        ]
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'jacoco'
    id 'signing'
    id 'com.github.kt3k.coveralls' version '2.12.0'
    id 'com.coditory.build' version '0.1.20' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0' apply false
}

repositories {
    mavenCentral()
}

description = 'Distributed Lock Library for JVM'

allprojects {
    group = 'com.coditory.sherlock'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'com.coditory.build'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }

    // make nicer archive and module names
    archivesBaseName = rootProject.name + '-' + project.name
    ext.moduleName = archivesBaseName.replaceAll('-', '.')

    dependencies {
        testImplementation "ch.qos.logback:logback-core:$versions.logback"
        testImplementation "ch.qos.logback:logback-classic:$versions.logback"
        testImplementation "org.spockframework:spock-core:$versions.spock"
    }
}

apply from: "gradle/coverage.gradle"

if (project.hasProperty("publish")) {
    apply from: "gradle/publish.gradle"
}
