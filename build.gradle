buildscript {
    ext {
        versions = [
                reactor        : '3.4.22',
                rxjava         : '2.2.21',
                mongodb        : '4.7.1',
                mongodbReactive: '4.7.1',
                slf4j          : '2.0.0',
                logback        : '1.4.0',
                postgresql     : '42.5.0',
                mysql          : '8.0.30',
                // test dependencies:
                jsonAssert     : '1.5.1',
                testContainers : '1.17.3',
                spock          : '2.3-groovy-4.0'
        ]
        notPublishableModules = [
                ':api',
                ':sample',
                ':mongo',
                ':tests'
        ]
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'jacoco'
    id 'signing'
    id 'com.github.kt3k.coveralls' version '2.12.0'
    id 'com.coditory.build' version '0.1.19' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0' apply false
}

repositories {
    mavenCentral()
}

description = 'Distributed Lock Library for JVM'

allprojects {
    group = 'com.coditory.sherlock'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'com.coditory.build'
    sourceCompatibility = 11

    // make nicer archive and module names
    archivesBaseName = rootProject.name + '-' + project.name
    ext.moduleName = archivesBaseName.replaceAll('-', '.')

    dependencies {
        testImplementation "ch.qos.logback:logback-core:$versions.logback"
        testImplementation "ch.qos.logback:logback-classic:$versions.logback"
        testImplementation "org.spockframework:spock-core:$versions.spock"
    }
}

apply from: "gradle/coverage.gradle"

if (project.hasProperty("publish")) {
    apply from: "gradle/publish.gradle"
}
